#!/bin/sh
#
# Parse the output of kernel-efi-warnings and write a luv-test-manager
# compatible log to stdout.
awk '/Test:/ {
	prev_test = test;
	test=$2;
	if (!parsing_tests) {
		parsing_tests = 1;
		prev_test = test;
	} else {
		printf ("1.0 kernel-efi-warnings %s END\n", prev_test);
	}

	printf ("1.0 kernel-efi-warnings %s START\n", test);
	fflush("");
    }

    /PASS/ {
	printf ("1.0 kernel-efi-warnings %s RESULT 1 0 0 0 0 0\n", test);
	fflush("");
    }

    /SKIP/ {
        printf ("1.0 kernel-efi-warnings %s RESULT 0 0 0 0 1 0\n", test);
        fflush("");
    }

    /FAIL/ {
	switch ($2) {
	case "[LOW]":
		severity = 1
		break;
	case "[MEDIUM]":
		severity = 2
		break;
	case "[HIGH]":
		severity = 3
		break;
	default:
		severity = -1;
	}
	printf ("1.0 kernel-efi-warnings %s RESULT 0 1 %d 0 0 0\n", test, \
	        severity);
	fflush("");
    }

    {
	printf ("1.0 kernel-efi-warnings %s INFO %s\n", test, $0);
	fflush("");
    }

    END {
	printf ("1.0 kernel-efi-warnings %s END\n", test);
	fflush("");
    }
'
