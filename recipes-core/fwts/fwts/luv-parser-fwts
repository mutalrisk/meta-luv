#!/bin/sh
#
# Parse the output of fwts and write a luv-test-manager compatible log
# to stdout.

# - cut out blank lines
# - strip the summary at the end, it's duplicate info
# - only include info about test passes/fails, etc
# - print in luv-test-manager format

awk '!/^summary/ && !/^fwts/ {
	if ($2 == ";HED") {
		# Some test cases issue more than one ;HED tag. Only issue a
		# START tag we begin parsing a new test case
		if (!parsing_test) {
			parsing_test = 1;
			test = $1
		        "date +%H:%M:%S:%N" | getline current_time;
		        close ("date +%H:%M:%S:%N")
			printf ("1.1 %s fwts %s START\n", current_time, test);
			fflush("");
		}
	}

	# FWTS uses blank lines to delimit the end of test case
	if (!NF) {
		if (parsing_test) {
			printf ("1.1 %s fwts %s END\n", current_time, test);
			parsing_test = 0;
			fflush("");
		}
	} else
		printf ("1.1 %s fwts %s INFO %s\n", current_time, $1, substr($0, index($0, $2)));
}

!/^summary/ && !/^fwts/ && /\;SUM.*passed/ {
	"date +%H:%M:%S:%N" | getline current_time;
	close ("date +%H:%M:%S:%N")

	printf ("1.1 %s fwts %s RESULT %d %d %d %d %d\n",
		current_time, $1, $3, $5, $9, $11, $7);
	fflush("");
}'
